#!/usr/bin/perl

use strict;
use warnings;
use Getopt::Long;
use Set::IntSpan::Fast;

use FindBin;
use lib "$FindBin::RealBin/../lib";

use GAL::Annotation;

#-----------------------------------------------------------------------------
#----------------------------------- MAIN ------------------------------------
#-----------------------------------------------------------------------------

my $usage = "

Synopsis:

gene_CDS_footprint features.gff3

Description:

This script will print gene ID and the footprint of the CDSs for that gene.

";

my ($help);

my $opt_success = GetOptions('help|h'            => \$help,
			      );

if (! $opt_success) {
    print STDERR join ' : ', ('FATAL',
                              'command_line_parse_error',
                              'Use gal_CDS_footprint --help to see correct usage');
}

if ($help) {
 print $usage;
 exit(0);
}

my ($gff3_file) = @ARGV;

die "$usage\n\nFATAL : missing_gff3_file : GFF3 file required\n" unless $gff3_file;

my $annotation = GAL::Annotation->new($gff3_file);

my $features = $annotation->features;

# Do the search and get an interator for all matching features
my $genes = $features->search({type => 'gene'});

my ($gene_count, $coding_count);
# Iterate over the features
GENE:
while (my $gene = $genes->next) {
    my $set = Set::IntSpan::Fast->new();
    my @children;
    $gene->get_recursive_children(\@children);
    my @CDSs = grep {$_->type eq 'CDS'} @children;
    next GENE unless scalar @CDSs;
    for my $CDS (@CDSs) {
	$set->add_range($CDS->start, $CDS->end);
    }
    my $gene_id = $gene->feature_id;
    my $footprint = scalar $set->as_array;
    print "$gene_id\t$footprint\n";
}
